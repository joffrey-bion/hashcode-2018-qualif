apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

group = 'org.luxons.hashcode'
version = '1.0.0'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile 'org.hildan.hashcode:hashcode-utils:5.0.0'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'junit:junit:4.+'
}

mainClassName = 'org.luxons.hashcode.Main'

task srcZip(type: Zip, dependsOn: tasks.build) {
    classifier = 'src'
    from sourceSets*.allSource
    destinationDir file("$rootDir/outputs/")
}

task generateOutputs(type: JavaExec, dependsOn: tasks.build) {
    args("inputs/a_example.in", "inputs/b_should_be_easy.in", "inputs/c_no_hurry.in", "inputs/d_metropolis.in",
            "inputs/e_high_bonus.in")
    description = "Hashcode output generation"
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
}

task startWebdriverManager(type: Exec) {
    commandLine 'webdriver-manager.cmd', 'start'
}

task runProtractor(type: Exec) {
    commandLine 'protractor.cmd', "$rootDir/deploy/conf.js"
}

task submit(dependsOn: [tasks.srcZip, tasks.generateOutputs]) {}
tasks.generateOutputs.mustRunAfter(tasks.srcZip)
tasks.runProtractor.mustRunAfter(tasks.generateOutputs)
